Let's make a game!
	name:Mega Man the Musical
 	by:Sike Mack
	desc:Beat the Robot Masters. Upgrade yourself. Save the world. AND DO IT IN SONG.
	created:6/20/16
	updated:10/13/2024
	version:5.0.0

Settings
	tiling background:https://s19.postimg.cc/j86u5agub/Background_2.png
	building cost increase:115%
	building cost refund:60%
	spritesheet:icons, 48 by 48, https://file.garden/ZsNT8FBfzxAupLGa/Mega%20Man%20the%20Musical/Spritesheet.png

Layout
	*main
		contains:res, buttons
		*res
			contains:Resources
			class:fullWidth
		*buttons
			contains:Buttons
  	*log
		  contains:Log
		  class:halfWidth
	*store
		contains:buildings, upgrades
		ps:show
		*buildings
			contains:BulkDisplay, Buildings
			header:<t>Robots</t>
			tooltip origin:left
		*upgrades
			contains:Upgrades
			header:<t>Upgrades</t>
			costs:hide
			names:hide

Buttons
	*makeZenny
		name:Make Zenny
		desc:What, did you think screws were accepted as currency everywhere? No, if you're going to put on a production, you'll need cold hard cash.
		on click:
			if (1poz < 1)
				yield 1 zenny
			/*
			else if (yashichiBuster=0)
				yield 1poz zenny
			*/
  			else
  				yield (zenny:ps*0.3) zenny
  		  		if (chance(0.01%))
  		    			yield 1 yashichi
  		  		end
  			end
  			anim icon wobble
		end
		class:bigButton
		no text
		icon:https://file.garden/ZsNT8FBfzxAupLGa/Mega%20Man%20the%20Musical/Zenny%20Bag.png
		show clicks

Resources
	*zenny
		name:Zenny
		desc:The aforementioned cold hard cash.
		icon:icons[0,0] icons[4,0]
		show earned
	*publicity
		name:Publicity
		desc:The best gauge for how well the show is going.
		icon:icons[0,0] icons[3,0]
		hidden when 0
		show earned
	*counter
		name:Seconds
		desc:Time functionality may be more implicit now, but still.
		icon:icons[0,0] icons[6,2]
		always hidden
		show earned
	*yashichi
		name:Yashichi
		desc:The most valuable resource available, it harnesses wind power to generate massive amounts of energy.
		icon:icons[0,0] icons[6,0]
		hidden
		show earned
	*1poz
		name:1% of Zenny
		icon:icons[0,0] icons[4,0]
		is always:(zenny:ps*0.01)
		always hidden
	*1pop
		name:1% of Publicity
		icon:icons[0,0] icons[3,0]
		is always:(publicity:ps*0.01)
		always hidden
	*starforceppt
		name:Starforce Earned Publicity
		desc:How much Publicity your Starforce have earned.
		icon:icons[0,0] icons[3,0]
		always hidden
		show earned
	[i:robotResources %name="Fire" %icon=4,2]

Buildings
	*timer
		name:Timer
		desc:I can't say I fully understand it.
		icon:icons[0,0] icons[6,2]
		always hidden
		start with:1
		on tick:yield 1 counter
		on tick:yield 0.00001157407407407407 yashichi
		on start:log Welcome!
		cost:0 zenny
	*starforce
		name:Starforce
		desc:A basic promotional tool. Doesn't reel in any cash by itself, but once you hire others on...</>Your [this] Starforce are earning <b>[starforceppt:ps]</b> Publicity each second. <b>[starforceppt]</b> Publicity earned so far.
		icon:icons[0,0] icons[3,0]
		on tick:yield 0.05 publicity
		on tick:yield 0.05 starforceppt
		passive:
			multiply yield of FireMan by 1.05
		end
		cost increase:135%
		cost:25 zenny
	[i:robotMaster %name="Fire" %code="DLN." %number="007" %flavor="Master of pyrotechnics, and well versed fighter, Fire Man will bring the heat to the stage like no other." %icon="4,2" %z="2.5" %p="0.04" %cost="20"]

Upgrades
	[i:unlockUpgrade %robot="Fire" %name="Fire Extinguisher" %flavor="Why did you not already have this? This is basic fire safety. Now you're ready in case a fire breaks out." %icon="4,2" %req="1 starforce" %boost="3" %cost="20"]

Achievements
	[i:robotAchievements %robot="Fire" %name10="Heat Miser" %name25="Mr. Sun" %name50="Green Christmas" %name75="12000Â°" %name100="Starting to Melt" %icon="4,2"]

Shinies

Includes
	*include robotMaster %name %title="Man" %textColor="d82800" %code="DWN." %number %flavor %icon %z %p %cost
		*[%name][%title]
			name:[%name] [%title]
			desc:<b><#[%textColor]>[%code] [%number]</#></></b>Currently at Level [this], [%name] [%title] is earning <b>[[%name]zpt:ps]</b> Zenny and <b>[[%name]ppt:ps]</b> Publicity each second.</><b>[[%name]zpt]</b> Zenny and <b>[[%name]ppt]</b> Publicity earned so far.<q>[%flavor]</q>
			icon:icons[0,0] icons[[%icon]]
			req:have [%name]Unlock
			on tick:
				yield [%z] zenny
				yield [%z] [%name]zpt
				yield [%p] publicity
				yield [%p] [%name]ppt
			end
			cost:[%cost] zenny
	*include unlockUpgrade %robot %name %title="Man" %flavor %icon %req %boost %cost
		*[%robot]Unlock
			name:[%name]
			desc:Boosts Starforce production by [%boost]0%, and unlocks [%robot] [%title].<q>[%flavor]</q>
			icon:icons[0,0] icons[[%icon]]
			req:[%req]
			passive:
				multiply yield of starforce by 1.[%boost]
				grant 1 [%robot][%title]
			end
			cost:[%cost] zenny
	*include robotResources %name %title="Man" %icon
		*[%name]zpt
			name:[%name] [%title]'s Earned Zenny
			desc:How much Zenny [%name] [%title] has earned.
			icon:icons[0,0] icons[[%icon]] icons[4,0]
			always hidden
			show earned
		*[%name]ppt
			name:[%name] [%title]'s Earned Publicity
			desc:How much Publicity [%name] [%title] has earned.
			icon:icons[0,0] icons[[%icon]] icons[3,0]
			always hidden
			show earned
	*include robotAchievements %robot %title="Man" %name10 %name25 %name50 %name75 %name100 %flavor10="" %flavor25="" %flavor50="" %flavor75="" %flavor100="" %icon
		*[%robot]10
			name:[%name10]
			desc:Upgrade [%robot] [%title] <b>10</b> times.[%flavor10]
			icon:icons[0,0] icons[[%icon]]
			req:10 [%robot][%title]
		*[%robot]25
			name:[%name25]
			desc:Upgrade [%robot] [%title] <b>25</b> times.[%flavor25]
			icon:icons[0,0] icons[[%icon]]
			req:25 [%robot][%title]
		*[%robot]50
			name:[%name50]
			desc:Upgrade [%robot] [%title] <b>50</b> times.[%flavor50]
			icon:icons[0,0] icons[[%icon]]
			req:50 [%robot][%title]
		*[%robot]75
			name:[%name75]
			desc:Upgrade [%robot] [%title] <b>75</b> times.[%flavor75]
			icon:icons[0,0] icons[[%icon]]
			req:75 [%robot][%title]
		*[%robot]100
			name:[%name100]
			desc:Upgrade [%robot] [%title] <b>100</b> times.[%flavor100]
			icon:icons[0,0] icons[[%icon]]
			req:100 [%robot][%title]
