Let's make a game!
	name:Mega Man the Musical
 	by:Sike Mack
	desc:Beat the Robot Masters. Upgrade yourself. Save the world. AND DO IT IN SONG.
	created:6/20/16
	updated:10/17/2024
	version:5.0.0

Settings
	tiling background:https://s19.postimg.cc/j86u5agub/Background_2.png
	building cost increase:115%
	building cost refund:60%
	spritesheet:icons, 48 by 48, https://file.garden/ZsNT8FBfzxAupLGa/Mega%20Man%20the%20Musical/Spritesheet.png

Layout
	*main
		contains:res, buttons
		*res
			contains:Resources
			class:fullWidth
		*buttons
			contains:Buttons
  	*log
		  contains:Log
		  class:halfWidth
	*store
		contains:buildings, upgrades
		ps:show
		*buildings
			contains:BulkDisplay, Buildings
			header:<t>Robots</t>
			tooltip origin:left
		*upgrades
			contains:Upgrades
			header:<t>Upgrades</t>
			costs:hide
			names:hide

Buttons
	*makeZenny
		name:Make Zenny
		desc:What, did you think screws were accepted as currency everywhere? No, if you're going to put on a production, you'll need cold hard cash.
		on click:
			if (1poz < 1)
				yield 1 zenny
			else if (yashichiBuster=0)
				yield 1poz zenny
  			else
  				yield (zenny:ps*0.3) zenny
  		  		if (chance(0.01%))
  		    			yield 1 yashichi
  		  		end
  			end
  			anim icon wobble
		end
		class:bigButton
		no text
		icon:https://file.garden/ZsNT8FBfzxAupLGa/Mega%20Man%20the%20Musical/Zenny%20Bag.png
		show clicks

Resources
	*zenny
		name:Zenny
		desc:The aforementioned cold hard cash.
		icon:icons[0,0] icons[4,0]
		show earned
	*publicity
		name:Publicity
		desc:The best gauge for how well the show is going.
		icon:icons[0,0] icons[3,0]
		hidden when 0
		show earned
	*counter
		name:Seconds
		desc:Time functionality may be more implicit now, but still.
		icon:icons[0,0] icons[6,2]
		always hidden
		show earned
	*yashichi
		name:Yashichi
		desc:The most valuable resource available, it harnesses wind power to generate massive amounts of energy.
		icon:icons[0,0] icons[6,0]
		hidden
		show earned
	*canClick|canClicks
	  	name:? Can Clicks
	  	desc:How many ? Cans have been clicked.
	  	icon:icons[0,0] icons[14,0]
	  	always hidden
	  	show earned
	*1poz
		name:1% of Zenny
		icon:icons[0,0] icons[4,0]
		is always:(zenny:ps*0.01)
		always hidden
	*1pop
		name:1% of Publicity
		icon:icons[0,0] icons[3,0]
		is always:(publicity:ps*0.01)
		always hidden
	*gearppt
		name:Gear Earned Publicity
		desc:How much Publicity your Gears have earned.
		icon:icons[0,0] icons[3,0]
		always hidden
		show earned
	[i:robotResources %name="Fire" %icon="4,2"]
	[i:robotResources %name="Mega" %icon="0,1"]

Buildings
	*timer
		name:Timer
		desc:I can't say I fully understand it.
		icon:icons[0,0] icons[6,2]
		always hidden
		start with:1
		on tick:yield 1 counter
		on tick:yield 0.00001157407407407407 yashichi
		on start:log Welcome!
		cost:0 zenny
	*gear|gears
		name:Gear|Gears
		desc:A basic promotional tool. Doesn't reel in any cash by itself, but once you hire others on...</>Your [?(this=0)|[this] Gears are][?(this=1)|[this] Gear is][?(this>1)|[this] Gears are] earning <b>[gearppt:ps]</b> Publicity each second. <b>[gearppt]</b> Publicity earned so far.
		icon:icons[0,0] icons[3,0]
		on tick:yield 0.05 publicity
		on tick:yield 0.05 gearppt
		[i:gearBoost %boost="1.05" %boostPlus="1.07"]
		cost increase:135%
		cost:25 zenny
	[i:robotMaster %name="Fire" %code="DLN." %number="007" %flavor="Master of pyrotechnics, and well versed fighter, Fire Man will bring the heat to the stage like no other." %icon="4,2" %z="2.5" %p="0.04" %cost="20"]
	[i:robotMaster %name="Mega" %textColor="0070e8" %code="DLN." %number="001" %flavor="The star of the show." %icon="0,1" %z="5000" %p="0.1" %increase="135"%cost="950000"]

Upgrades
	//Zenny Buster
	[i:zennyUpgrade %id="1" %name="Zenny Buster" %boost="1.25" %boostPercent="25" %flavor="A high tech blaster arm to boost your click power." %req="10 zenny" %cost="100 zenny"]
	[i:zennyUpgrade %id="2" %name="Oiled Buster" %boost="1.25" %boostPercent="25" %flavor="Your Zenny Buster has been oiled." %req="have zennyUpgrade1, 100 zenny" %cost="200 zenny"]
	[i:zennyUpgrade %id="3" %name="Buster Screws" %boost="1.25" %boostPercent="25" %flavor="Some tighter screws for your Zenny Buster." %req="have zennyUpgrade2, 1000 zenny" %cost="2000 zenny"]
	[i:zennyUpgrade %id="4" %name="Buster Power" %boost="1.5" %boostPercent="50" %flavor="A newfangled power cell for your Zenny Buster." %req="have zennyUpgrade3, 10000 zenny" %cost="20000 zenny"]
	[i:zennyUpgrade %id="5" %name="Ceratanium Buster" %boost="1.5" %boostPercent="50" %flavor="Your Zenny Buster has been reinforced with Ceratanium, making it stronger than ever." %req="have zennyUpgrade4, 100000 zenny" %cost="200000 zenny"]
	[i:zennyUpgrade %id="6" %name="Fancy LED Light" %boost="1.5" %boostPercent="50" %flavor="It blinks!" %req="have zennyUpgrade5, 1000000 zenny" %cost="2000000 zenny"]
	[i:zennyUpgrade %id="7" %name="Improved Buster Design" %boost="2" %boostPercent="100" %flavor="At the cost of some Publicity, your Zenny Buster has been revamped to suit the latest styles." %req="have zennyUpgrade6, 200000 publicity" %cost="400000 publicity"]
	[i:zennyUpgrade %id="8" %name="Retro Buster Design" %boost="2" %boostPercent="100" %flavor="Y'know, that newer design is nice and all, but retro is where it's at. Why do you think we're using this art style? The Buster is re-revamped with an old-school look." %req="have zennyUpgrade7, 1000000000 zenny" %cost="2000000000 zenny"]
	*yashichiBuster
		name:Yashichi Buster
		desc:Locks manual Zenny production to 1/3 of your Zenny Per Second.<q>Your Zenny Buster's final form, you won't need to upgrade it ever again.</q>
		icon:icons[1,0] icons[13,0]
		req:have zennyUpgrade8, 1000000000000 zenny
		cost:1 yashichi
	//Fire Man
	[i:unlockUpgrade %robot="Fire" %name="Fire Extinguisher" %flavor="Why did you not already have this? This is basic fire safety. Now you're ready in case a fire breaks out." %icon="4,2" %req="1 gear" %boost="3" %cost="20"]
	[i:gearUpgrade %name="Fire" %flavor="Smells like smoke." %icon="4,2" %req="25" %cost="100000"]
	[i:robotUpgrades %robot="Fire" %name1="Fireproofing" %name2="Waterproofing" %flavor1="Once again, you really should have had this earlier. Should prevent any more accidental fires." %flavor2="You've achieved enough fame to get Fire Man burning too hot to be extinguished." %icon="4,2" %cost1="200000" %cost2="120000"]
	[i:specialWeapon %robot="Fire" %name="Fire Storm" %flavor="Fire Storm creates a ring of fire and a powerful fireball, while also creating a major stage hazard." %icon="4,2" %target="Bomb"]
	//Mega Man
	[i:unlockUpgrade %robot="Mega" %name="Weapon Copy System" %flavor="Allows your star to mimic the weapons of the Robot Masters. Oh wait, the star. Knew we were forgetting something." %icon="0,1" %req="50 FireMan" %boost="7" %cost="250000000"]
	[i:gearUpgrade %name="Mega" %flavor="Feels like you've accomplished something." %icon="0,1" %req="25" %cost="2500000000"]
	//? Cans
	*canUpgradeOne
		name:Frequent Shipments
		desc:? Cans will appear 20% faster.
		icon:icons[0,0] icons[14,0]
		req:3 questionCan:clicks
		passive:multiply frequency of questionCan by 0.8
		cost:10000 zenny
	*canUpgradeTwo
		name:? Can Coating
		desc:Treated with a special chemical solution, ? Cans will last 20% longer before disappearing.
		icon:icons[0,0] icons[14,0]
		req:10 questionCan:clicks
		passive:multiply duration of questionCan by 1.2
		cost:1000000 zenny
	*canUpgradeThree
		name:Rigged Odds
		desc:Through a bit of probability manipulation, the odds of a ? Can yielding a Yashichi are 5% higher.
		icon:icons[1,0] icons[14,0]
		req:20 questionCan:clicks, yashichievOne=1
		cost:1 yashichi

Achievements
	[i:robotAchievements %robot="Fire" %name10="Heat Miser" %name25="Mr. Sun" %name50="Green Christmas" %name75="12000Â°" %name100="Starting to Melt" %icon="4,2"]
	[i:robotAchievements %robot="Mega" %name10="Rockin'" %name25="Rollin'" %name50="Bustin'" %name75="Mega" %name100="Man" %icon="0,1"]
	*canAchieveOne
		name:Questionable Contents
		desc:Click your first ? Can.
		icon:icons[0,0] icons[14,0]
		req:canClicks >= 1
	*canAchieveTwo
		name:Gotten Lucky Yet?
		desc:Click 10 ? Cans.
		icon:icons[0,0] icons[14,0]
		req:canClicks >= 10
	*canAchieveThree
		name:The Long Game
		desc:Click 100 ? Cans.
		icon:icons[0,0] icons[14,0]
		req:canClicks >= 100
	*yashichievOne
		name:Never Forget Your First
		desc:Earn your first Yashichi.
		icon:icons[0,0] icons[6,0]
		req:(yashichi:earned) >= 1
		on earn:show yashichi

Shinies
	*questionCan
		icon:icons[0,0] icons[14,0]
		frequency:120
		//frequency:5
		movement:anywhere growShrink
		duration:30
		on click:
			yield 1 canClick
			if (canClicks = 3) toast The ? Can reveals a new upgrade.
			if (canClicks = 10) toast The ? Can reveals a new upgrade.
			if (canClicks = 20) toast The ? Can reveals a new upgrade.
			/*
			if (chance(10%) and timeUnlock=0 and roll>=10)
				toast The ? Can reveals a new upgrade.
				yield timeUnlock
			else if (chance(10%) and oilUnlock=0 and timeUnlock=1 and roll>=10)
				toast The ? Can reveals a new upgrade.
				yield oilUnlock
			else if (chance(10%) and bondUnlock=0 and timeUnlock=1 and oilUnlock=1 and roll>=10)
				toast The ? Can reveals a new upgrade.
				yield bondUnlock
			else if (chance(5%) and enkerUnlock=0 and (metalMan>=10, airMan>=10, bubbleMan>=10, quickMan>=10, crashMan>=10, flashMan>=10, heatMan>=10, woodMan>=10))
				toast The ? Can reveals a new upgrade.
				yield enkerUnlock
			else if (chance(10%) and quakeUnlock=0 and rush>=10)
				toast The ? Can reveals a new upgrade.
				yield quakeUnlock
			else if (chance(5%) and quintUnlock=0 and protoMan>=10)
				toast The ? Can reveals a new upgrade.
				yield quintUnlock
			else if (chance(10%) and vesperUnlock=0 and quakeWoman>=1 and protoMan>=25)
				toast The ? Can reveals a new upgrade.
				yield vesperUnlock
			else if (chance(15%) and canUpgradeThree=1)
			*/
			if (chance(15%) and canUpgradeThree=1)
				toast The ? Can grants you 1 Yashichi.
				yield 1 yashichi
			else if (chance(10%))
				toast The ? Can grants you 1 Yashichi.
				yield 1 yashichi
			else
				$amount=max(100,random(zenny:ps*60*2,zenny:ps*60*5))
				toast The ? Can grants you<//><b>[$amount] Zenny</b>!
				yield $amount zenny
				//yield $amount canzpt
			end
		end

Includes
	*include robotMaster %name %title="Man" %textColor="d82800" %code="DWN." %number %flavor %icon %z %p %increase="115" %cost
		*[%name][%title]
			name:[%name] [%title]
			desc:<b><#[%textColor]>[%code] [%number]</#></></b>Currently at Level [this], [%name] [%title] is earning <b>[[%name]zpt:ps]</b> Zenny and <b>[[%name]ppt:ps]</b> Publicity each second.</><b>[[%name]zpt]</b> Zenny and <b>[[%name]ppt]</b> Publicity earned so far.<q>[%flavor]</q>
			icon:icons[0,0] icons[[%icon]]
			req:have [%name]Unlock
			on tick:
				yield [%z] zenny
				yield [%z] [%name]zpt
				yield [%p] publicity
				yield [%p] [%name]ppt
			end
			cost increase:[%increase]%
			cost:[%cost] zenny
	*include zennyUpgrade %id %name %boost %boostPercent %flavor %req %cost
		*zennyUpgrade[%id]
			name:[%name]
			desc:Boosts manual Zenny Production by [%boostPercent]%.<q>[%flavor]</q>
			icon:icons[0,0] icons[13,0]
			req:[%req]
			passive:
				if (yashichiBuster=0)
					multiply zenny yield of makeZenny by [%boost]
				end
			end
			cost:[%cost]
	*include unlockUpgrade %robot %name %title="Man" %flavor %icon %req %boost %cost
		*[%robot]Unlock
			name:[%name]
			desc:Boosts Gear production by [%boost]0%, and unlocks [%robot] [%title].<q>[%flavor]</q>
			icon:icons[0,0] icons[[%icon]]
			req:[%req]
			passive:
				multiply yield of gears by 1.[%boost]
				grant 1 [%robot][%title]
			end
			cost:[%cost] zenny
	*include gearUpgrade %name %title="Man" %flavor %icon %req %cost
		*[%name]Gear
			name:[%name] Gear
			desc:Boosts [%name] [%title]'s production by 30%.<q>A special Gear variant branded around [%name] [%title]![%flavor]</q>
			icon:icons[2,0] icons[0,0] icons[[%icon]]
			req:[%req] gears, 15 [%name][%title]
			passive:multiply yield of [%name][%title] by 1.3
			cost:[%cost] zenny
	*include robotUpgrades %robot %name1 %name2 %title="Man" %flavor1 %flavor2 %icon %cost1 %cost2
		*[%robot]Upgrade25
			name:[%name1]
			desc:Boosts [%robot] [%title]'s production by 100%.<q>[%flavor1]</q>
			icon:icons[0,0] icons[[%icon]]
			req:25 [%robot][%title]
			passive:multiply yield of [%robot][%title] by 2
			cost:[%cost1] zenny
		*[%robot]Upgrade50
			name:[%name2]
			desc:Boosts [%robot] [%title]'s production by 100%.<q>[%flavor2]</q>
			icon:icons[0,0] icons[[%icon]]
			req:50 [%robot][%title]
			passive:multiply yield of [%robot][%title] by 2
			cost:[%cost2] publicity
	*include specialWeapon %robot %name %title="Man" %flavor %icon %target %targetTitle
		*[%robot]Weapon
			name:[%name]
			desc:Boosts [%robot] [%title] and Mega Man's production by 100%.<q>[%flavor]</q>
			icon:icons[1,0] icons[[%icon]]
			req:75 [%robot][%title], 1 MegaMan
			passive:
				multiply yield of [%robot][%title] by 2
				multiply yield of MegaMan by 2
				//multiply cost of [%target][%targetTitle] by .95
			end
			cost:1 yashichi
	*include robotResources %name %title="Man" %icon
		*[%name]zpt
			name:[%name] [%title]'s Earned Zenny
			desc:How much Zenny [%name] [%title] has earned.
			icon:icons[0,0] icons[[%icon]] icons[4,0]
			always hidden
			show earned
		*[%name]ppt
			name:[%name] [%title]'s Earned Publicity
			desc:How much Publicity [%name] [%title] has earned.
			icon:icons[2,0] icons[0,0] icons[[%icon]]
			always hidden
			show earned
	*include robotAchievements %robot %title="Man" %name10 %name25 %name50 %name75 %name100 %flavor10="." %flavor25="." %flavor50="." %flavor75="." %flavor100="." %icon
		*[%robot]Achiev10
			name:[%name10]
			desc:Upgrade [%robot] [%title] <b>10</b> times[%flavor10]
			icon:icons[0,0] icons[[%icon]]
			req:10 [%robot][%title]
		*[%robot]Achiev25
			name:[%name25]
			desc:Upgrade [%robot] [%title] <b>25</b> times[%flavor25]
			icon:icons[0,0] icons[[%icon]]
			req:25 [%robot][%title]
		*[%robot]Achiev50
			name:[%name50]
			desc:Upgrade [%robot] [%title] <b>50</b> times[%flavor50]
			icon:icons[0,0] icons[[%icon]]
			req:50 [%robot][%title]
		*[%robot]Achiev75
			name:[%name75]
			desc:Upgrade [%robot] [%title] <b>75</b> times[%flavor75]
			icon:icons[0,0] icons[[%icon]]
			req:75 [%robot][%title]
		*[%robot]Achiev100
			name:[%name100]
			desc:Upgrade [%robot] [%title] <b>100</b> times[%flavor100]
			icon:icons[0,0] icons[[%icon]]
			req:100 [%robot][%title]
	*include gearBoost %boost %boostPlus
		passive:
			//multiply yield of Roll by [%boostPlus]
			//multiply yield of CutMan by [%boost]
			//multiply yield of GutsMan by [%boost]
			//multiply yield of IceMan by [%boost]
			//multiply yield of BombMan by [%boost]
			multiply yield of FireMan by [%boost]
			//multiply yield of ElecMan by [%boost]
			//multiply yield of TimeMan by [%boost]
			//multiply yield of OilMan by [%boost]
			//multiply yield of BondMan by [%boost]
			multiply yield of MegaMan by [%boostPlus]
			//multiply yield of MetalMan by [%boost]
			//multiply yield of AirMan by [%boost]
			//multiply yield of BubbleMan by [%boost]
			//multiply yield of QuickMan by [%boost]
			//multiply yield of CrashMan by [%boost]
			//multiply yield of FlashMan by [%boost]
			//multiply yield of HeatMan by [%boost]
			//multiply yield of WoodMan by [%boost]
			//multiply yield of Enker by [%boost]
			//multiply yield of Rush by [%boostPlus]
			//multiply yield of QuakeWoman by [%boostPlus]
			//multiply yield of NeedleMan by [%boost]
			//multiply yield of MagnetMan by [%boost]
			//multiply yield of GeminiMan by [%boost]
			//multiply yield of HardMan by [%boost]
			//multiply yield of TopMan by [%boost]
			//multiply yield of SnakeMan by [%boost]
			//multiply yield of SparkMan by [%boost]
			//multiply yield of ShadowMan by [%boost]
			//multiply yield of ProtoMan by [%boostPlus]
			//multiply yield of Quint by [%boost]
			//multiply yield of VesperWoman by [%boostPlus]
		end
